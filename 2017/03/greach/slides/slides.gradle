buildscript {
    dependencies {
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

plugins {
  id 'groovy'
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'com.github.jruby-gradle.base' version '1.2.1'
  id 'org.ysb33r.vfs' version '1.0'
}

version = '1.0.0-SNAPSHOT'

ext {
    revealjsVersion           = '3.2.0'
    asciidoctorBackendVersion = 'master'
    downloadDir               = new File(buildDir,'download')
    templateDir               = new File(downloadDir,'templates')
    revealjsDir               = new File(downloadDir,'reveal.js')
}

repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:slim:3.0.6'
    gems 'rubygems:thread_safe:0.3.5'
}

task(download){
  doLast {
    mkdir downloadDir
    vfs {
      cp("zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
         templateDir,
         recursive:true,
         overwrite:true)

      cp("zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
         revealjsDir,
         recursive:true,
         overwrite:true)
    }
  }
}

download {
  group 'documentation'
  description "Download extra revealjs resources"

  inputs.files downloadDir
  outputs.dir templateDir
  outputs.dir revealjsDir
}

asciidoctorj {
  version = '1.5.4'
}

asciidoctor {

  dependsOn jrubyPrepare, download

	sources {
		include 'index.adoc'
	}

	resources {
		from (sourceDir) {
			include 'images/**'
		}
		from (downloadDir) {
			include 'reveal.js/**'
		}
	}

    backends 'revealjs'

	attributes	'build-gradle': file('build.gradle'),
				'sourcedir': project.sourceSets.main.java.srcDirs[0],
				'endpoint-url': 'http://example.org',
				'source-highlighter': 'coderay',
				'imagesdir': './images',
				'toc': 'left',
				'icons': 'font',
				'setanchors': '',
				'idprefix': '',
				'idseparator': '-',
				'docinfo1': '',
				'revealjs_theme': 'beige',
				'revealjs_transition': 'linear',
				'revealjs_history': 'true',
				'revealjs_slideNumber': 'true'

	options template_dirs : [new File(templateDir,'templates/slim').absolutePath ]
}
