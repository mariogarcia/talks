buildscript {
  dependencies {
    classpath 'commons-httpclient:commons-httpclient:3.1'
    classpath 'org.asciidoctor:asciidoctorj-diagram:1.3.1'
  }
}

plugins {
  id 'groovy'
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'com.github.jruby-gradle.base' version '1.4.0'
  id 'org.ysb33r.vfs' version '1.0'
}

version = '1.0.0-SNAPSHOT'

ext {
  revealjsVersion           = '3.2.0'
  asciidoctorBackendVersion = 'maint-1.0.x'
  downloadDir               = new File(buildDir,'download')
  templateDir               = new File(downloadDir,'templates')
  revealjsDir               = new File(downloadDir,'reveal.js')
}

repositories {
  jcenter()
}

defaultTasks 'asciidoctor'

task(download){
  doLast {
    mkdir downloadDir
    vfs {
      cp("zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
         templateDir,
         recursive:true,
         overwrite:true)

      cp("zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
         revealjsDir,
         recursive:true,
         overwrite:true)
    }
  }
}

download {
  group 'documentation'
  description "Download extra revealjs resources"

  inputs.files downloadDir
  outputs.dir templateDir
  outputs.dir revealjsDir
}

asciidoctorj {
  version = '1.5.4'
}

asciidoctor {
    dependsOn download
	sources {
		include 'index.adoc'
	}
	resources {
		from (sourceDir) {
			include 'images/**'
		}
		from (downloadDir) {
			include 'reveal.js/**'
		}
	}
  requires 'asciidoctor-diagram'
  backends 'revealjs'
	attributes	'build-gradle': file('build.gradle'),
				'sourcedir': project.sourceSets.main.java.srcDirs[0],
				'endpoint-url': 'http://example.org',
				'source-highlighter': 'coderay',
				'imagesdir': './images',
				'toc': 'left',
				'icons': 'font',
				'setanchors': '',
				'idprefix': '',
				'idseparator': '-',
				'docinfo1': '',
				'revealjs_theme': 'simple',
				'revealjs_transition': 'slide',
				'revealjs_history': 'true',
				'revealjs_slideNumber': 'true'
	options template_dirs : [new File(templateDir,'templates/slim').absolutePath ]
}
